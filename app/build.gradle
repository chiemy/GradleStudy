// google的Android开发团队编写的插件，能够提供所有关于Android应用和依赖库的构建，打包和测试。
apply plugin: 'com.android.application'
apply from: 'build_name.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.chiemy.example.gradlestudy"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file("../chiemy.jks")
            storePassword "chiemy1989"
            keyAlias "chiemytest"
            keyPassword "chiemy1989"
        }
    }

    buildTypes {
        release {
            // 剔除无用资源, 这个是依赖代码混淆的, minifyEnabled也要设置为true
            shrinkResources true
            // 是否优化/混淆
            minifyEnabled true
            // 混淆文件规则文件, proguard-android.txt是默认的混淆设置(在Android SDK tools/proguard/ 文件夹下), proguard-rules.pro'是提供给我们添加混淆规则的
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "API_URL", "\"http://release.example.com/api\""
            // 编译时动态替换资源
            // 注意, 第一个参数是小写的 string
            resValue "string", "app_name", "GradleStudy"
        }

        debug {
            // debug 版包名以.debug结尾, 这样我们的手机上正式版和测试版就可以共存了
            applicationIdSuffix ".debug"
            versionNameSuffix "_debug"
            signingConfig signingConfigs.release
            buildConfigField "String", "API_URL", "\"http://debug.example.com/api\""
            resValue "string", "app_name", "GradleStudy Debug"
        }

//        custom1 {
//            applicationIdSuffix ".custom1"
//        }
    }

    productFlavors{
        chiemy{
            // 替换manifest中的占位符UMENG_CHANNEL_VALUE 为 own
            manifestPlaceholders= [UMENG_CHANNEL_VALUE : "own"]
            minSdkVersion 15

        }

        market_360{
            manifestPlaceholders= [UMENG_CHANNEL_VALUE : "360"]
        }

        wandoujia{
            manifestPlaceholders= [UMENG_CHANNEL_VALUE : "wandoujia"]
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    // 配置文件在, 项目根目录下的 dependency.gradle 文件夹内, 在根目录下的 build.gradle 通过apply from:引入
    // 这种方式在有多个module时非常有用, 避免修改依赖时, 需要修改多个module的问题
    compile rootProject.ext.libSupportAppcompat
    // compile rootProject.ext.dependencies.appcompatV7
}
